element template nimh_Rt in1 in2 outp outm rt_value = model,k

ref nu in1,in2
electrical outp,outm
output nu rt_value
number k = 1.0000

struc {
	string	file="ess_nimh7_Rt.ai_dat"
	number	interp=0,
		extrap[4]=[1,1,1,1],
		fill[2]=[2,0],
		density[2]=[100,100]
	} model=()
{
foreign tlu

number	dim=2,
	datap[*],
	sp1[*],
	sp2[*]

val nu SOC,Temperature
val r Rt
val r Rt_adj
val nu voltage_drop
val nu voltage_drop_squared

parameters {
	datap=tlu(0,dim,model->file,datap,model->interp,
	model->extrap,model->fill)
	sp1=tlu(1,addr(datap),1,model->density[1])
	sp2=tlu(1,addr(datap),2,model->density[2])
	}

values {
	SOC=in1
	Temperature=in2
	Rt=tlu(2,addr(datap),SOC,Temperature)
	voltage_drop=v(outp)-v(outm)
	voltage_drop_squared=(v(outp)-v(outm))*(v(outp)-v(outm))
	Rt_adj = k*Rt
	}

control_section {
	pl_set(Rt,(SOC,Temperature))
	sample_points(SOC,sp1)
	sample_points(Temperature,sp2)
	}

equations {
	i(outp->outm)+=(v(outp)-v(outm))/Rt_adj
	rt_value=(v(outp)-v(outm))*(v(outp)-v(outm))/Rt_adj
	}
}

